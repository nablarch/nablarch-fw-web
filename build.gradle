group = 'com.nablarch.framework'
version = '1.4.2'
description = '画面オンライン処理方式を実現する。'

buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:${coberturaPluginVersion}"
  }
}

apply plugin: 'com.nablarch.dev.nablarch-build'
apply plugin: 'com.nablarch.dev.nablarch-maven-deploy'
apply plugin: 'com.nablarch.dev.nablarch-sonarqube'
apply plugin: 'com.nablarch.dev.nablarch-version'
apply from: 'https://github.com/nablarch/nablarch-gradle-plugin/blob/develop/scripts/cobertura/cobertura-0.0.1.gradle?raw=true'

apply plugin: 'idea'
apply plugin: 'eclipse'

// ビルド時のJavaバージョンを指定
sourceCompatibility=JavaVersion.VERSION_1_6
targetCompatibility=JavaVersion.VERSION_1_6

configurations.all {
  resolutionStrategy {
    force 'log4j:log4j:1.2.17'
  }
}

dependencies {
  compile "com.nablarch.framework:nablarch-core-beans:${nablarchCoreBeansVersion}"
  compile "com.nablarch.framework:nablarch-fw:${nablarchFwVersion}"
  compile "com.nablarch.framework:nablarch-core-validation:${nablarchCoreValidationVersion}"
  compile "com.nablarch.framework:nablarch-core-repository:${nablarchCoreRepositoryVersion}"
  compile "com.nablarch.framework:nablarch-common-encryption:${nablarchCommonEncryptionVersion}"
  provided "com.nablarch.framework:nablarch-core-validation-ee:${nablarchCoreValidationEeVersion}"

  // Servlet/JSP dependencies.
  provided 'javax.servlet:servlet-api:2.5'

  // integration test
  testRuntime "com.nablarch.framework:nablarch-core-jdbc:${nablarchCoreJdbcVersion}"
  testRuntime "com.nablarch.framework:nablarch-common-jdbc:${nablarchCommonJdbcVersion}"

  // testFW
  testCompile "com.nablarch.framework:nablarch-testing:${nablarchTestingVersion}"

  // test api
  testCompile 'org.jmockit:jmockit:1.13'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'

  testRuntime files('src/test/java/')
  testRuntime 'org.hibernate:hibernate-validator:5.2.4.Final'
  testRuntime 'log4j:log4j:1.2.17'
}

test {
  jvmArgs (
          '-Xms16m',
          '-Xmx32m',
          '-XX:PermSize=32m',
          '-XX:+UseParallelGC',
          '-Dfile.encoding=UTF-8',
          '-XX:-UseSplitVerifier'
  )


  beforeTest { descriptor ->
    logger.lifecycle("Running test: " + descriptor)
  }

}

tasks.sonarRunner {
  dependsOn = ['coverage']
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}
